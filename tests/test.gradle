
import groovy.json.JsonSlurper

buildscript {
  repositories { 
    mavenCentral() 
  }
  dependencies { 
    classpath 'com.aestasit.infrastructure.sshoogr:sshoogr:0.9.18'
    classpath 'com.aestasit.infrastructure.sshoogr:sshoogr-gradle:0.9.18'
  }
}

apply plugin: 'base'
apply plugin: 'secureShell'


ext {

  stateFile = new File('terraform.tfstate')

  isVagrant = {
    project.hasProperty('infra') && project.infra == 'vagrant'
  }

  getTerraformState = {
    stateFile.exists() ? new JsonSlurper().parse(stateFile) : [:]
  }

  getServerIp = { 
    if (project.isVagrant()) {
      return '192.168.23.10'
    } else {
      return project.getTerraformState()?.modules?.first()?.resources?.findAll { it.key.startsWith 'aws_instance.test_server' }?.collect { it?.value?.primary?.attributes?.public_ip }?.find() ?: '127.0.0.1'
    }    
  }

  getServerUser = { 
    if (project.isVagrant()) {
      return 'vagrant'
    } else {
      return 'ubuntu'
    }    
  }

}

sshOptions {
  defaultPort       = 22
  defaultPassword   = ''
  if (project.isVagrant()) {
    defaultKeyFile  = file('.vagrant/machines/default/virtualbox/private_key')    
  } else {
    defaultKeyFile  = file('test.key')
  }
  reuseConnection   = true
  trustUnknownHosts = true
  verbose           = true
  execOptions {
    showCommand     = true
    showOutput      = true
  }
}


task createServer(type: Exec) {
  if (project.isVagrant()) {
    commandLine 'vagrant', 'up'
  } else {
    commandLine 'terraform', 'apply'
  }
}

task uploadTests {
  doLast {
    remoteSession {
      user = project.getServerUser()
      host = project.getServerIp()
      scp {
        from { localFile 'test.bats' }
        into { remoteDir '/tmp' }
      }
    }
  }
}

uploadTests.mustRunAfter createServer

task runTests {
  doLast {
    remoteSession {
      user = project.getServerUser()
      host = project.getServerIp()
      exec 'bats -t /tmp/test.bats'
      exec 'cat /tmp/puppet.output'
    }
  }
}

runTests.mustRunAfter uploadTests

task destroyServer(type: Exec) {
  if (project.isVagrant()) {
    commandLine 'vagrant', 'destroy', '--force'
  } else {
    commandLine 'terraform', 'destroy', '-force'
  }
}

destroyServer.mustRunAfter runTests
